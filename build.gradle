plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'jacoco'
    id "org.sonarqube" version "4.0.0.2929"
}

group = 'TAPP.org'
version = '0.0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.postgresql:postgresql'
    testImplementation 'org.testcontainers:postgresql:1.18.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}


jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "Shikamaakuma_TAPP"
        property "sonar.organization", "shikamaakuma"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jar {
    enabled = false // With that, no *-plain.jar will be generated
}
tasks.register('flutterBuild') {
    File flutterDir = project.projectDir

    doFirst {
        println 'Flutter'
        flutterDir = new File(project.projectDir.toString(), "frontend")
        println flutterDir

        exec {
            workingDir flutterDir.toString()
            commandLine '/home/dominik/software/flutter/flutter/bin/flutter', 'build', 'web'
        }
    }
}

tasks.register('flutterCopy', Copy) {
    dependsOn(flutterBuild)
    from layout.projectDirectory.dir('frontend').dir('build').dir('web')
    include "**/*.*"
    into layout.projectDirectory.dir('src').dir('main').dir('resources').dir('static')
}

tasks.register('flutterServerStart') {
    dependsOn(flutterCopy)
    dependsOn(build)
}

